#include <ESP8266WiFi.h>        // Einbinden der WiFi-Bibliothek für den ESP8266
#include <PubSubClient.h>       // Einbinden der MQTT-Bibliothek

const char* ssid = "Opa";                                   //WLAN Name
const char* password = "OmaundOpa";                         //WLAN Password
const char* mqtt_server = "192.168.178.25";                 //IP Adresse des Raspberry Pi
const char* key_topic = "edu/key";                       //Variable fürs key-Topic

//----------------------------------------------//
//Bitte IMMER zu der Nummer unterm ESP abändern //
const char* team_topic = "edu/team1";                       //Variable fürs Sensor-Topic
const char* esp_name = "team1";                             //Name des ESPs
//Das Topic, welches der ESP subscriben soll
const char* sub_topic = "edu/sub";                          
//----------------------------------------------//



WiFiClient espClient;           // Erstellen eines WiFiClient-Objekts
PubSubClient client(espClient); // Erstellen eines MQTT Client-Objekts

// Diese Funktion verbindet den ESP8266 mit dem WLAN
void setup_wifi() {
  delay(10);
  Serial.println("Verbinde mit WLAN...");
  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  Serial.println("");
  Serial.println("WLAN verbunden");
  Serial.println("IP-Adresse: ");
  Serial.println(WiFi.localIP());
}

// Diese Funktion wird aufgerufen, wenn eine Nachricht auf einem abonnierten Topic ankommt
void callback(char* topic, byte* payload, unsigned int length) {
  // Erstelle einen String aus dem Payload
  char message[length + 1];
  for (unsigned int i = 0; i < length; i++) {
    message[i] = (char)payload[i];
  }
  message[length] = '\0'; // Füge ein Nullzeichen hinzu, um das Ende des Strings anzuzeigen

  // Konvertiere den Payload-String in einen Integer
  int messageValue = atoi(message);

  // Speichere das Topic als String
  String topicStr = String(topic);

  // Jetzt können Topic und MessageValue in der Konsole ausgegeben werden
  Serial.print("Nachricht erhalten unter [");
  Serial.print(topicStr);
  Serial.print("] mit dem Wert ");
  Serial.println(messageValue);



  //----------------------------------------------//
  //Hier kommt euer selbstgeschriebener Code rein //
  //----------------------------------------------//


}

// Diese Funktion stellt die Verbindung mit dem MQTT-Broker her und wird bei Verbindungsabbruch ausgeführt, bis die Broker verbindung wieder steht.
void reconnect() {
 
  while (!client.connected()) {                         // Schleife, bis wir mit dem MQTT Broker verbunden sind
    Serial.print("Versuche, eine MQTT-Verbindung herzustellen...");

    // Verbindungsaufbau
    if (client.connect(esp_name)) {                    //Verbindung zum Broker 
      Serial.println("verbunden");
      client.subscribe(sub_topic);                      //Das gewünschte Topic wird abonniert

    } else {
      Serial.print("Fehler, rc=");
      Serial.print(client.state());                     //Fehlercode ausgeben
      Serial.println(" Neuer Versuch in 5 Sekunden");
      delay(5000);                                      // 5 Sekunden warten, bevor erneut versucht wird
    }
  }
}

void setup() {
  Serial.begin(115200); // Starten der seriellen Kommunikation mit 115200 Baud
  setup_wifi();         // Aufrufen der Funktion zum Verbinden mit dem WLAN
  client.setServer(mqtt_server, 1883); // Einstellen des MQTT-Servers und Ports
  client.setCallback(callback);        // Festlegen der Callback-Funktion

  pinMode(A0, INPUT); // Initialisieren des A0 Pins als Eingang
  pinMode(D1, INPUT_PULLUP); // Initialisieren des D1 Pins als Eingang
}

void loop() {
  if (!client.connected()) {
    reconnect(); // Stellt sicher, dass wir mit dem MQTT-Broker verbunden sind
  }
  client.loop(); // Diese Funktion muss regelmäßig aufgerufen werden, um intern MQTT-Kommunikationsaufgaben durchzuführen

  static long lastMsg = 0;
  long now = millis();
  if (now - lastMsg > 2500) { // Prüft, ob 2,5 Sekunden vergangen sind
    lastMsg = now;

    // Auslesen der Sensoren und Speichern in Variablen
    int sensorValueA0 = analogRead(A0);
    int sensorValueD1 = digitalRead(D1);

    // Erstellen von Nachrichten-Strings für die Sensorwerte
    char msgA0[8];
    char msgD1[8];
    itoa(sensorValueA0, msgA0, 10); // Konvertierung des Integer-Wertes in einen String
    itoa(sensorValueD1, msgD1, 10); // Konvertierung des Integer-Wertes in einen String

    // Ausgabe der Sensorwerte auf der seriellen Konsole
    Serial.print("Sensorwert A0: ");
    Serial.println(msgA0);
    Serial.print("Sensorwert D1: ");
    Serial.println(msgD1);




    //----------------------------------------------//
    // Veröffentlichen der Sensorwerte auf den entsprechenden MQTT-Topics
    client.publish(team_topic, msgA0);
    client.publish(key_topic, msgD1);
    //----------------------------------------------//





  }
}
